buildscript {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.18.0'
        classpath "com.gradle.publish:plugin-publish-plugin:1.2.0"
    }
}

apply plugin: 'groovy'

def publishMaven = false
if (publishMaven) {
    apply plugin: "com.vanniktech.maven.publish"
} else {
    apply plugin: 'signing'
    apply plugin: 'com.gradle.plugin-publish'

    group = 'io.github.qq549631030'
    version = VERSION_NAME

    gradlePlugin {
        plugins {
            androidJunkCode {
                id = 'io.github.qq549631030.android-junk-code'
                implementationClass = 'cn.hx.plugin.junkcode.plugin.AndroidJunkCodePlugin'
                displayName = 'AndroidJunkCode plugin'
                description = POM_DESCRIPTION
            }
        }
    }

    pluginBundle {
        website = POM_URL
        vcsUrl = POM_SCM_URL
        description = POM_DESCRIPTION
        tags = ['android', 'generate', 'junk', 'code']
    }

    publishing {
        publications {
            pluginMaven(MavenPublication) {
                artifactId = POM_ARTIFACT_ID
                pom {
                    name = POM_NAME
                    description = POM_DESCRIPTION
                    inceptionYear = POM_INCEPTION_YEAR
                    url = POM_URL
                    licenses {
                        license {
                            name = POM_LICENCE_NAME
                            url = POM_LICENCE_URL
                        }
                    }
                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                            url = POM_DEVELOPER_URL
                        }
                    }
                    scm {
                        url = POM_SCM_URL
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_DEV_CONNECTION
                    }
                }
            }
        }
    }
}

java {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
}

dependencies {
    implementation gradleApi()
    implementation 'com.squareup:javapoet:1.13.0'
    compileOnly 'com.android.tools.build:gradle-api:7.0.0'
}